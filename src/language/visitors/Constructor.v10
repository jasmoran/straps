func Constructor Constructor_new() {
	return Constructor_new(None<BlockNode>)
}

class virtual Constructor < Visitor (Option<BlockNode> pb) {
	func Option<Node> visit(ClassNode n) {
		var pb = this.pb.expect("Class defined outside of block")

		var pos = n.position.clone("Constructor")
		var new_obj = StringNode_new(nIDENTIFIER, pos, Type_new(n.name), "obj")

		var size = n.fields.length
		if (n.vtable.is_some() == true) size++
		size << 3

		# Create allocate call + assign and append to block
		var rando = LinkedList<RetNode>_new()
		rando.append(
			StringNode_new(nNUMBER, pos, Type_new("Number"), size.toString())
		)

		var block = BlockNode_new(pos, Scope_new("ConstructorScope", pb.scope)).append(
			BinaryOperationNode_new(
				pos,
				ASSIGN,
				new_obj,
				CallNode_new(
					pos,
					StringNode_new(nIDENTIFIER, pos, Type_new(n.name, CAST), "allocate"),
					rando
				)
			)
		)

		# Loop through fields
		for (field in n.fields) {
			# Assign passed values to this field and append to block
			block.append(
				BinaryOperationNode_new(
					pos,
					ASSIGN,
					BinaryOperationNode_new(pos, DOT, new_obj, field.clone()),
					field.clone()
				)
			)
		}

		var args = n.fields.shallow_clone()

		# Add vtable
		if (n.vtable.is_some() == true) {
			# Add vtable to fields
			var vt_ident = StringNode_new(nIDENTIFIER, pos, Type_new("void"), "vt")
			n.fields.prepend(vt_ident)

			var tabname = "_vtable_".clone(n.name.length).append(n.name)

			# Assign vtable
			block.append(
				BinaryOperationNode_new(
					pos,
					ASSIGN,
					BinaryOperationNode_new(Type_new("void", CAST), pos, DOT, new_obj, vt_ident),
					StringNode_new(nNASM, pos, Type_new("void", CAST), tabname)
				)
			)
		}

		# Create return node and append to block
		block.append(
			ReturnNode_new(pos, new_obj)
		)

		# Add new_obj to block scope
		block.scope.set(
			new_obj.string,
			LocalVariableNode_new(
				new_obj.ret_type,
				new_obj.position,
				0,
				new_obj.string
			)
		)

		# Create _new function and append to parent block
		pb.append(
			FunctionNode_new(
				nFUNCTION,
				pos,
				Type_new(n.name),
				n.name.clone(4).append("_new"),
				args,
				block,
				1
			)
		)

		return None<Node>
	}

	func Option<BlockNode> visit(BlockNode n) {
		var old_pb = this.pb
		this.pb = Some(n)

		n.accept_children(this)

		this.pb = old_pb

		return None<BlockNode>
	}
}
