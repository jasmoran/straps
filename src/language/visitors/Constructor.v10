func Constructor Constructor_new()(ListNode ln, Node n) {
	ln = 0
	n = 0
	var Visitor v = 0
	var cnst = Constructor_new(v, ln, ln, n)
	cnst.v = cnst
	return cnst
}

class virtual Constructor < Visitor (Visitor v, ListNode pb, ListNode block, Node new_obj) {
	func void visit(ClassNode n)(Number size) {
		this.new_obj = StringNode_new(nIDENTIFIER, Type_new(n.name), "obj")

		size = n.fields.children.length
		if (n.vtable != 0) size++
		size << 3

		# Create allocate call + assign and append to block
		var rando = LinkedList<StringNode>_new()
		rando.append(
			StringNode_new(nNUMBER, Type_new("Number"), size.toString())
		)
		this.block = ListNode_new(nBLOCK).append(
			BinaryOperationNode_new(
				ASSIGN,
				this.new_obj,
				CallNode_new(
					StringNode_new(nIDENTIFIER, Type_new(n.name), "allocate").toNode(),
					rando
				).toNode()
			 ).toNode()
		)

		# Loop through fields
		n.fields.accept_children(this.v)

		var ListNode args = ListNode_new(nLIST, n.fields.children.shallow_clone())

		# Add vtable
		if (n.vtable != 0) {
			# Add vtable to fields
			var Node vt_ident = StringNode_new(nIDENTIFIER, Type_new(), "vt")
			n.fields.prepend(vt_ident)

			var tabname = "_vtable_".clone(n.name.length).append(n.name)

			# Assign vtable
			this.block.append(
				BinaryOperationNode_new(
					ASSIGN,
					BinaryOperationNode_new(DOT, this.new_obj, vt_ident).toNode(),
					StringNode_new(nNASM, Type_new(), tabname).toNode()
				).toNode()
			)
		}

		# Create return node and append to block
		this.block.append(
			ReturnNode_new(this.new_obj).toNode()
		)

		# Create _new function and append to parent block
		this.pb.append(
			FunctionNode_new(
				n.position.clone("Constructor"),
				Type_new(n.name),
				n.name.clone(4).append("_new"),
				args,
				ListNode_new(nLIST).append(this.new_obj),
				this.block
			).toNode()
		)

		# Clear out block (acts as flag for StringNode method)
		this.block = 0

  	return 0
	}

	func void visit(ListNode n)(ListNode old_pb) {
		old_pb = this.pb
		if (n.type == nBLOCK)
			this.pb = n

		n.accept_children(this.v)

		this.pb = old_pb

  	return 0
	}

	func void visit(StringNode n)() {
		if (this.block != 0) {
			# Assign passed values to this field and append to block
			this.block.append(
				BinaryOperationNode_new(
					ASSIGN,
					BinaryOperationNode_new(DOT, this.new_obj, n.toNode()).toNode(),
					n.toNode()
				).toNode()
			)
		}

  	return 0
	}
}
