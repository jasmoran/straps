class virtual Inherit < Visitor (Reporting report) {
	func void remove_duplicates(LinkedList<StringNode> list, StringNode field) {
		for (sn in list) {
			# Check for matches
			if (sn.string.equal(field.string) == true) {
				list.remove(sn_iter.curr.unwrap())
			}
		}
	}

	func Option<Node> visit(ClassNode n) {
		if (n.parent.is_none() == true) return None<Node>
		var parent = n.parent.unwrap()

		# Resolve parent type
		parent.resolve(scope)

		if (parent.res.is_some() == true) {
			if (parent.res.unwrap().vtable.is_some() == true)
			if (n.vtable.is_none() == true) {
				this.report.printNonVirtualChild(n)
			}

			for (node in parent.res.unwrap().fields.iter().reverse()) {
				if (node.type != nIDENTIFIER) error()
				var sn = node.to(StringNode)

				# Don't inherit vtable
				if (sn.string.equal("vt") == false) {
					this.remove_duplicates(n.fields, sn)
					n.fields.prepend(sn)
				}
			}
		}

		return None<Node>
	}
}
