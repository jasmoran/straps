func StaticNew StaticNew_new() {
  return StaticNew_new(None<BlockNode>)
}

class virtual StaticNew < Visitor (Option<BlockNode> pb) {
  func Option<Node> visit(ClassNode n) {
    var pb = this.pb.expect("Attempted to attach static class outside of block")

    # Find any init methods
    var inits = LinkedList<FunctionNode>_new()
    for (nmeth in n.block.children) {
      var meth = nmeth.to(FunctionNode)

      if (meth.name.equal("init") == true) {
        inits.append(meth)
      }
    }

    # Check if any init methods have been defined
    if (inits.length == 0) return None<Node>

    # Calculate size of allocation
    var size = n.fields.length
    if (n.vtable.is_some() == true) size++
    size << 3

    var pos = n.position.clone("StaticNew")

    # Create obj identifier
    var obj = StringNode_new(nIDENTIFIER, pos, Type_new(n.name), "obj")

    # Create allocation call
    var Node allocation = BinaryOperationNode_new(
      pos, ASSIGN,
      obj,
      CallNode_new(
        pos,
        StringNode_new(nIDENTIFIER, pos, Type_new(n.name, CAST), "allocate"),
        LinkedList<RetNode>_new().append(
          StringNode_new(nNUMBER, pos, Type_new("Number"), size.toString())
        )
      )
    )

    # Create return node
    var ret = ReturnNode_new(pos, obj)

    # Create static class block
    var block = BlockNode_new(pos, Scope_new("StaticClassScope", pb.scope))

    for (init in inits) {
      var args = init.args.shallow_clone().to(LinkedList<RetNode>)

      # Create init call
      var Node init_call = CallNode_new(
        pos,
        BinaryOperationNode_new(pos, DOT, obj,
          StringNode_new(nIDENTIFIER, pos, Type_new(), "init")),
        args
      )

      # Create block for new function
      var new_block = BlockNode_new(pos, Scope_new("StaticConstructorScope", Some(scope)))
                        .append(allocation)

      # Assign vtable
      if (n.vtable.is_some() == true) {
        var Node vt = StringNode_new(nIDENTIFIER, pos, Type_new(), "vt")
        var tabname = "_vtable_".clone(n.name.length).append(n.name)

        # Assign vtable
        new_block.append(
          BinaryOperationNode_new(
            pos,
            ASSIGN,
            BinaryOperationNode_new(pos, DOT, obj, vt),
            StringNode_new(nNASM, pos, Type_new(), tabname)
          )
        )
      }

      # Call init and return
      new_block.append(init_call)
      new_block.append(ret)

      # Create args to new function
      var new_args = LinkedList<StringNode>_new()
      new_args = init.args.shallow_clone()

      # Add obj to block scope
      new_block.scope.set(
        obj.string,
        LocalVariableNode_new(
          obj.ret_type,
          obj.position,
          0,
          obj.string
        )
      )

      block.append(
        FunctionNode_new(
          nFUNCTION,
          pos,
          Type_new(n.name),
          "new",
          new_args,
          new_block,
          NullString,
          1
        )
      )
    }

    # Create and attach static class
    var name = n.name.clone(6).append("Static")
    var static_class = ClassNode_new(pos, name, n.fields, block)
    pb.prepend(static_class)

    # Add static class to scope
    scope.set(name, static_class)

    return None<Node>
  }

  func Option<BlockNode> visit(BlockNode n) {
    var old_pb = this.pb
    this.pb = Some(n)

    n.accept_children(this)

    this.pb = old_pb

    return None<BlockNode>
  }
}
