include "src/language/Node.v10"
include "src/language/Tokenizer.v10"

func Parser Parser_new(String stream_name, Stream input)() {
	return Parser_new(stream_name, Tokenizer_new(stream_name, input))
}

func Node Parser_parse(String stream_name, Stream input)(Parser p) {
	p = Parser_new(stream_name, input)
	return p.parse()
}

class Parser(String name, Tokenizer t) {
	func Node nt_class()(String name, ListNode fields, ListNode block) {
		this.t.consume(CLASS)

		var pos = this.t.get_pos()

		var Map<String_FunctionNode> vtable = 0
		if (this.t.peek() == VIRTUAL) {
			this.t.consume(VIRTUAL)
			vtable = Map<String_FunctionNode>_new(32)
		}

		name = this.t.consume(IDENTIFIER)

		var String parent = 0
		if (this.t.peek() == LESS) {
			this.t.consume(LESS)
			parent = this.t.consume(IDENTIFIER)
		}

		fields = this.typed_list(nLIST)
		block = this.block(nBLOCK)

		return ClassNode_new(pos, name, fields, block, vtable, Type_new(parent))
	}

	func Node nt_enum()(String name, ListNode values) {
		this.t.consume(ENUM)

		var pos = this.t.get_pos()

		name = this.t.consume(IDENTIFIER)
		values = this.block(nBLOCK)

		return EnumNode_new(pos, name, values)
	}

	func Node nt_function()(String ret_type, String name, ListNode args, ListNode vrbls, ListNode block) {
		this.t.consume(FUNC)

		var pos = this.t.get_pos()

		ret_type = this.t.consume(IDENTIFIER)
		name = this.t.consume(IDENTIFIER)
		args = this.typed_list(nLIST)
		if (this.t.peek() == PAREN_OPEN) {
			vrbls = this.typed_list(nLIST)
		} else {
			vrbls = ListNode_new(nLIST, this.t.get_pos())
		}
		block = this.block(nBLOCK)

		return FunctionNode_new(pos, Type_new(ret_type), name, args, vrbls, block)
	}

	func StringNode nt_identifier() {
		var ident = this.t.consume(IDENTIFIER)
		var pos = this.t.get_pos()
		return StringNode_new(nIDENTIFIER, pos, Type_new(), ident)
	}

	func Node nt_if()(IfNode node, Node expr, ListNode block_if) {
		this.t.consume(IF)

		var pos = this.t.get_pos()

		expr = this.expression()
		block_if = this.block(nBLOCK)
		node = IfNode_new(pos, expr, block_if)

		this.skip_newlines()
		if (this.t.peek() == ELSE) {
			this.t.consume(ELSE)
			node.block_else = this.block(nBLOCK)
		}

		return node
	}

	func Node nt_include()(String path, Parser parser) {
		this.t.consume(INCLUDE)
		path = this.t.consume(STRING)
		parser = Parser_new(path, File_open(path))
		parser.t.macros = this.t.macros
		return parser.parse()
	}

	func Node nt_nasm()(Node node) {
		this.t.consume(NASM)
		var pos = this.t.get_pos()
		this.t.consume(PAREN_OPEN)
		node = StringNode_new(nNASM, pos, Type_new("void"), this.t.consume(STRING))
		this.t.consume(PAREN_CLOSE)
		return node
	}






	func Node parse()(ListNode l) {
		var pos = this.t.get_pos()
		l = ListNode_new(nBLOCK, pos)

		loop {
			var expr = this.expression()
			if (expr == EOF) { break }
			l.children.append(expr)
		}

		return l
	}

	func ListNode block(NodeType t)(ListNode l) {
		this.skip_newlines()

		var peek = this.t.peek()
		var pos = this.t.get_pos()
		l = ListNode_new(t, pos)

		if (peek == CURLY_OPEN) {
			this.t.consume(CURLY_OPEN)

			loop {
				if (this.t.peek() == CURLY_CLOSE) { break }
				if (this.t.peek() == EOF) { break }
				l.children.append(this.expression())
				this.skip_newlines()
			}

			this.t.consume(CURLY_CLOSE)
		} else {
			l.children.append(this.expression())
		}

		return l
	}

	func ListNode typed_list(NodeType t)(ListNode l, String type, String vrbl) {
		this.t.consume(PAREN_OPEN)
		l = ListNode_new(t, this.t.get_pos())

		loop {
			if (this.t.peek() == PAREN_CLOSE) { break }

			type = this.t.consume(IDENTIFIER)
			var pos = this.t.get_pos()
			vrbl = this.t.consume(IDENTIFIER)
			var n = StringNode_new(nIDENTIFIER, pos, Type_new(type), vrbl)

			l.append(n.toNode())

			if (this.t.peek() != COMMA) { break }

			this.t.consume(COMMA)
		}

		this.t.consume(PAREN_CLOSE)

		return l
	}

	func LinkedList<Node> expression_list() {
		this.t.consume(PAREN_OPEN)
		var l = LinkedList<Node>_new()

		loop {
			if (this.t.peek() == PAREN_CLOSE) { break }
			l.append(this.expression())
			if (this.t.peek() != COMMA) { break }
			this.t.consume(COMMA)
		}

		this.t.consume(PAREN_CLOSE)

		return l
	}





	func void skip_newlines()() {
		loop {
			if (this.t.peek() != NEW_LINE) break
			this.t.consume(NEW_LINE)
		}
	}

	func Node expression()(Node node, Token tk) {
		node = this.logic_expr()
		loop {
			tk = this.t.peek()
			if (tk != ASSIGN)
			if (tk != ADD)
			if (tk != SUB)
			if (tk != SHIFTL)
			if (tk != SHIFTR) break
			this.t.consume(tk)
			var pos = this.t.get_pos()
			node = BinaryOperationNode_new(pos, tk, node, this.logic_expr())
		}
		return node
	}

	func Node logic_expr()(Node node, Token tk) {
		node = this.comp_expr()
		loop {
			tk = this.t.peek()
			if (tk != AND)
			if (tk != OR) break
			this.t.consume(tk)
			var pos = this.t.get_pos()
			node = BinaryOperationNode_new(pos, tk, node, this.comp_expr())
		}
		return node
	}

	func Node comp_expr()(Node node, Token tk) {
		node = this.add_expr()
		loop {
			tk = this.t.peek()
			if (tk != EQUAL)
			if (tk != NOT_EQUAL)
			if (tk != GREATER)
			if (tk != GREATER_EQ)
			if (tk != LESS)
			if (tk != LESS_EQ) break
			this.t.consume(tk)
			var pos = this.t.get_pos()
			node = BinaryOperationNode_new(pos, tk, node, this.add_expr())
		}
		return node
	}

	func Node add_expr()(Node node, Token tk) {
		node = this.mult_expr()

		loop {
			tk = this.t.peek()
			if (tk != PLUS)
			if (tk != MINUS) break
			this.t.consume(tk)
			var pos = this.t.get_pos()
			node = BinaryOperationNode_new(pos, tk, node, this.mult_expr())
		}
		return node
	}

	func Node mult_expr()(Node node, Token tk) {
		node = this.snd_expr()
		loop {
			tk = this.t.peek()
			if (tk != ASTERISK)
			if (tk != FWD_SLASH) break
			this.t.consume(tk)
			var pos = this.t.get_pos()
			node = BinaryOperationNode_new(pos, tk, node, this.snd_expr())
		}
		return node
	}

	func Node snd_expr()(Node node, Token tk) {
		tk = this.t.peek()
		if (tk != PLUS)
		if (tk != MINUS)
		if (tk != NOT)
		if (tk != ASTERISK) return this.top_expr()

		this.t.consume(tk)
		var pos = this.t.get_pos()
		return UnaryOperationNode_new(pos, tk, this.snd_expr(), true)
	}

	func Node top_expr()(Node node, Token tk) {
		node = this.value()
		loop {
			tk = this.t.peek()
			var pos = this.t.get_pos()
			if (tk == PAREN_OPEN) {
				var LinkedList<StringNode> arglist = this.expression_list()
				node = CallNode_new(pos, node, arglist)
			} else if (tk == SQUARE_OPEN) {
				this.t.consume(SQUARE_OPEN)
				node = BinaryOperationNode_new(pos, SQUARE_OPEN, node, this.expression())
				this.t.consume(SQUARE_CLOSE)
			} else if (tk == DOT) {
				this.t.consume(DOT)
				node = BinaryOperationNode_new(pos, DOT, node, this.value())
			} else if (tk == INCREMENT) {
				this.t.consume(INCREMENT)
				node = UnaryOperationNode_new(pos, INCREMENT, node, false)
			} else if (tk == DECREMENT) {
				this.t.consume(DECREMENT)
				node = UnaryOperationNode_new(pos, DECREMENT, node, false)
			} else break
		}
		return node
	}

	func Node value()(Token tk, Node node, String raw_type, Type type, String vrbl) {
		this.skip_newlines()

		tk = this.t.peek()

		# Bracketed expression
		if (tk == PAREN_OPEN) {
			this.t.consume(PAREN_OPEN)
			node = this.expression()
			this.t.consume(PAREN_CLOSE)
			return node
		}

		if (tk == EOF) return EOF

		if (tk == BREAK) {
			this.t.consume(BREAK)
			return Node_new(nBREAK)
		}

		if (tk == CHARACTER) {
			var chr = this.t.consume(CHARACTER)
			var pos = this.t.get_pos()
			return StringNode_new(nCHARACTER, pos, Type_new("Character"), chr)
		}

		if (tk == CLASS) {
			return this.nt_class()
		}

		if (tk == ENUM) {
			return this.nt_enum()
		}

		if (tk == FOR) {
			this.t.consume(FOR)
			var pos = this.t.get_pos()
			this.t.consume(PAREN_OPEN)
			var item = this.nt_identifier()
			this.t.consume(IN)
			var iter = this.expression()
			this.t.consume(PAREN_CLOSE)

			return ForNode_new(pos, item, iter, this.block(nBLOCK))
		}

		if (tk == FUNC) {
			return this.nt_function()
		}

		if (tk == GLOBAL) {
			this.t.consume(GLOBAL)
			return this.typed_list(nGLOBAL)
		}

		if (tk == IDENTIFIER) {
			return this.nt_identifier()
		}

		if (tk == IF) {
			return this.nt_if()
		}

		if (tk == INCLUDE) {
			return this.nt_include()
		}

		if (tk == LOOP) {
			this.t.consume(LOOP)
			var pos = this.t.get_pos()
			return LoopNode_new(pos, this.block(nBLOCK))
		}

		if (tk == NASM) {
			return this.nt_nasm()
		}

		if (tk == NEXT) {
			this.t.consume(NEXT)
			return Node_new(nNEXT)
		}

		if (tk == NUMBER) {
			var num = this.t.consume(NUMBER)
			var pos = this.t.get_pos()
			return StringNode_new(nNUMBER, pos, Type_new("Number"), num)
		}

		if (tk == RETURN) {
			this.t.consume(RETURN)
			var pos = this.t.get_pos()
			return ReturnNode_new(pos, this.expression())
		}

		if (tk == STRING) {
			var str = this.t.consume(STRING)
			var pos = this.t.get_pos()
			return StringNode_new(nSTRING, pos, Type_new("String"), str)
		}

		# TODO: add SWITCH/CASE

		if (tk == SYS) {
			this.t.consume(SYS)
			return ListNode_new(nSYS, this.t.get_pos(), this.expression_list())
		}

		if (tk == VAR) {
			this.t.consume(VAR)
			var vr_pos = this.t.get_pos()
			raw_type = this.t.consume(IDENTIFIER)
			var ident_pos = this.t.get_pos()
			if (this.t.peek() == IDENTIFIER) {
				# Explicit
				vrbl = this.t.consume(IDENTIFIER)
				type = Type_new(raw_type)
			} else {
				# Inferring
				vrbl = raw_type
				type = Type_new()
				type.raw = 1
			}
			this.t.consume(ASSIGN)
			var ass_pos = this.t.get_pos()

			return UnaryOperationNode_new(vr_pos, type, VAR,
				BinaryOperationNode_new(Type_new(), ass_pos, ASSIGN,
					StringNode_new(nIDENTIFIER, ident_pos, type, vrbl).toNode(),
						this.expression()).toNode(), true)
		}

		# TODO: add WHILE

		error("Unknown value expression! (Token: ", tk.toString(), ")\n")
	}
}
