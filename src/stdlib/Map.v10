func Map Map_new(Number size)() {
	return Map_new(Array_new(size), Array_new(size))
}

class Map (Array keys, Array values) {
	func Number get_pos(String key) {
		var Any a = key
		nasm("mov rax, _String_equal_String_String")
		var Function f = rax
		return this.keys.find(a, f)
	}

	func Any get(String key) {
		var ix = this.get_pos(key)

		if (ix < 0) {
			return ix
		}

		return this.values.get(ix)
	}

	func void set(String key, Any value) {
		var Any a = key
		var ix = this.get_pos(key)

		if (ix < 0) {
			this.keys.append(a)
			this.values.append(value)
		} else {
			this.values.set(ix, value)
		}
	}

	func void clear()() {
		this.keys.clear()
		this.values.clear()
	}

	func void print_keys()(Number index, String key) {
		index = 0
		loop {
			if (this.keys.length == index) break
			key = this.keys.get(index)
			key.println()
			index++
		}
	}

	func Map shallow_clone() {
		return Map_new(this.keys.shallow_clone(), this.values.shallow_clone())
	}
}
